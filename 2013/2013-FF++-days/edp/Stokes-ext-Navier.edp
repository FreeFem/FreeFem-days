// Author: F. Hecht  
// jan 2012 Stationnary imcompressible Navier Stokes Equation with Newton method.
//  a round a 3d Cylinder 
// build the  Mesh
real R = 5,L=15;
border cc(t=0,2*pi){ x=cos(t)/2;y=sin(t)/2;label=1;}
border ce(t=pi/2,3*pi/2) { x=cos(t)*R;y=sin(t)*R;label=2;}
border beb(tt=0,1) { real t=tt^1.2; x= t*L; y= -R; label = 2;}
border beu(tt=1,0) { real t=tt^1.2; x= t*L; y= R; label = 2;}
border beo(t=-R,R) {  x= L; y= t; label = 3;}
border bei(t=-R/4,R/4) {  x= L/2; y= t; label = 0;}
mesh Th=buildmesh(cc(-50)+ce(30)+beb(20)+beu(20)+beo(10)+bei(10));
plot(Th);

func mT = [N.y,-N.x]*[N.y,-N.x]';
func mN = [N.x,N.y]*[N.x,N.y]';
real betaT = 1e6;
real betaN = 1e6;
// bounding box for the plot 
//func bb=[[-1,-2],[4,2]];
plot(Th,wait=1); 
fespace Vh(Th,P2); 
fespace Ph(Th,P1); 

Vh u1,u2,v1,v2;
Ph p,q;
real f1=1,f2=0;
 
macro DD(u,v) [ dx(u),dy(v), (dx(v)+dy(u))*0.5, (dx(v)+dy(u))*0.5 ] // 
macro Grad(u,v) [ dx(u),dy(v), dx(v), dy(u) ] // 
macro div(u1,u2) (dx(u1)+dy(u2) ) //
 
solve PMeca( [u1,u2,p],[v1,v2,q] ) =
int2d(Th) ( 2*DD(u1,u2)'*DD(v1,v2) - div(v1,v2)*p -div(u1,u2)*q)

+ int1d(Th,1)( betaT*[u1,u2]'*mT*[v1,v2]+  betaN*[u1,u2]'*mN*[v1,v2])
//+ on(1,u1=0,u2=0) 


-  int1d(Th,2,3)([f1,f2]'*[v1,v2]); 


cout << u1[].linfty << " " <<  u2[].linfty << " "<< p[].linfty << endl;

plot([u1,u2],p, wait=1,coef= 0.1/( u1[].linfty +  u2[].linfty), cmm=" meca 1"  );

solve PMath( [u1,u2,p],[v1,v2,q] ) =
int2d(Th) ( Grad(u1,u2)'*Grad(v1,v2) - div(v1,v2)*p -div(u1,u2)*q)

+ int1d(Th,1)( betaT*[u1,u2]'*mT*[v1,v2]+  betaN*[u1,u2]'*mN*[v1,v2])
//+ on(1,u1=0,u2=0) 

-  int1d(Th,2,3)([f1,f2]'*[v1,v2]); 


cout << u1[].linfty << " " <<  u2[].linfty << " "<< p[].linfty << endl;

plot([u1,u2],p, wait=1,coef= 0.1/( u1[].linfty +  u2[].linfty), cmm=" Math 1"  );

// Verif  B.C in pression curve   u.t = 0 , 
real pcl = 2; 
solve PMathP( [u1,u2,p],[v1,v2,q] ) =
int2d(Th) ( Grad(u1,u2)'*Grad(v1,v2) - div(v1,v2)*p -div(u1,u2)*q)
+ int1d(Th,1)( betaT*[u1,u2]'*mT*[v1,v2]+  betaN*[u1,u2]'*mN*[v1,v2])
+ int1d(Th,2)( betaT*[u1,u2]'*mT*[v1,v2] )
+on(3,u1=-(R-y)*(R+y)/R/R,u2=0)// 
+int1d(Th,2)( pcl*[N.x,N.y]'*[v1,v2]) // p = 1 on Gamma_1 
; 
plot([u1,u2],p, wait=1,coef= 0.3/( u1[].linfty +  u2[].linfty), cmm=" Math Pressur"  );
plot(p,nbiso=50,cmm="CL in  Math Pressur" );
