// 10 minutes dans le Train. 
load "medit" load "tetgen" load "mshmet"
include "cube.idp"
int[int]  Nxyz=[20,5,5];
real [int,int]  Bxyz=[[0.,5.],[0.,1.],[0.,1.]];
int [int,int]  Lxyz=[[1,2],[2,2],[2,2]];
mesh3 Th=Cube(Nxyz,Bxyz,Lxyz);
real E = 21.5e4;
real sigma = 0.29;
real mu = E/(2*(1+sigma));
real lambda = E*sigma/((1+sigma)*(1-2*sigma));
real gravity = -0.05;

fespace Vh(Th,[P1,P1,P1]);
fespace Hh(Th,[P1]);
Vh [u1,u2,u3], [v1,v2,v3];
cout << "lambda,mu,gravity ="<<lambda<< " " << mu << " " << gravity << endl;

real sqrt2=sqrt(2.);
macro epsilon(u1,u2,u3)  [dx(u1),dy(u2),dz(u3),(dz(u2)+dy(u3))/sqrt2,(dz(u1)+dx(u3))/sqrt2,(dy(u1)+dx(u2))/sqrt2] // EOM
macro div(u1,u2,u3) ( dx(u1)+dy(u2)+dz(u3) ) // EOM
  
problem Lame([u1,u2,u3],[v1,v2,v3])=
  int3d(Th)(  
	    lambda*div(u1,u2,u3)*div(v1,v2,v3)	
	    +2.*mu*( epsilon(u1,u2,u3)'*epsilon(v1,v2,v3) ) //')
	      )
  - int3d(Th) (gravity*v3)
  + on(1,u1=0,u2=0,u3=0)
  ;

real errm=5e-4;// level of error 
mesh3 Thm; 
for(int ii=0; ii<4; ii++)
{
Lame;
real dmax= u1[].max;
cout << " max deplacement = " << dmax << endl;
real coef= 0.1/dmax;
int[int] ref2=[1,0,2,0];
 Thm=movemesh3(Th,transfo=[x+u1*coef,y+u2*coef,z+u3*coef],refface=ref2);
Thm=change(Thm,refface=ref2);
plot(Th,Thm, wait=1,cmm="coef  amplification = "+coef );
//medit(" Th ",Thm);

 Hh h;
  h[]=mshmet(Th,[u1,u2,u3],normalization=1,aniso=0,nbregul=1,hmin=1e-3,hmax=0.3,err=errm);//loptions=MSHloptions,doptions=MSHdoptions);
  cout <<" h min, max = " <<  h[].min << " "<< h[].max << " " << h[].n << " " << Th.nv << endl;
 
  errm*= 0.5;// change the level of error
  cout << " Th" << Th.nv < " " << Th.nt << endl;
  Th=tetgreconstruction(Th,switch="raAQ",sizeofvolume=h*h*h/6.);
  
}
medit("Thm",Thm); 
